Option, ECHO=TRUE;
//////////////////////////////////////////////////////////////////////////////
// Input file for single bunch tracking through KURRI ADS FFAG ring         //
//////////////////////////////////////////////////////////////////////////////
Title,string="KURRI ADS FFAG example simulation using OPAL code";
Option, ASCIIDUMP=TRUE;

Edes=11.0/1000;
step_size_mm=10; // mm
n_turns=200.0003;
revolution_period=630.; // ns

gamma=(Edes+PMASS)/PMASS;
beta=sqrt(1-(1/gamma^2));
p0 = gamma*beta*PMASS;
brho = (PMASS*1.0e9*gamma*beta) / CLIGHT;

// print to terminal
value,{gamma,brho,Edes,beta,p0};

step_size=step_size_mm/beta/CLIGHT*1e5; // ns
max_steps=n_turns*revolution_period/step_size;
steps_per_turn=revolution_period/step_size;
frequency=1./revolution_period;

r_closed_orbit=4249.37+42353.2*Edes-1.06978e+06*Edes^2+1.50301e+07*Edes^3-8.37681e+07*Edes^4;
value,{step_size_mm, n_turns, revolution_period, step_size, max_steps, r_closed_orbit};

probe1: PROBE, xstart=0, xend=+6000., ystart=0, yend=0.;

// 4.60193762e+03 at 11 MeV
ringdef: RINGDEFINITION, HARMONIC_NUMBER=1, LATTICE_RINIT=4700.0,
         LATTICE_PHIINIT=0.0, LATTICE_THETAINIT=0.0, BEAM_PHIINIT=0., 
         BEAM_PRINIT=0.0, BEAM_RINIT=r_closed_orbit, SYMMETRY=1.0, RFFREQ=frequency,
         IS_CLOSED=true; 

// polynomial coefficients map energy 0.011+W [GeV] to frequency [GHz]
rf_f0=+0.00158279;
rf_f1=+0.0570222;
rf_f2=-0.785013;
rf_f3=+6.20161;
// (mean) change in energy [GeV] per time [ns]
dtdW=0.00001*rf_f0; // 0.0185 MeV = 18.5e-6 GeV per turn
phi=2.*PI*(0.250+(0.02969));

// polynomial coefficients map frequency [GHz] to time [ns]
rf_p0=rf_f0;
rf_p1=rf_f1*dtdW;
rf_p2=rf_f2*dtdW^2;
rf_p3=rf_f3*dtdW^3;

rf_frequency: POLYNOMIAL_TIME_DEPENDENCE, P0=rf_p0, P1=rf_p1, P2=rf_p2, P3=rf_p3;
rf_amplitude: POLYNOMIAL_TIME_DEPENDENCE, P0=1.;
rf_phase: POLYNOMIAL_TIME_DEPENDENCE, P0=phi;

patch_offset: LOCAL_CARTESIAN_OFFSET, end_position_x=0.114049, end_position_y=0., end_normal_x=1., end_normal_y=0.;
triplet: SBEND3D, FMAPFN="fieldmaps/tosca_map_f810_d1020_massaged.table", LENGTH_UNITS=10., FIELD_UNITS=1e-3;

rf_cavity: VARIABLE_RF_CAVITY, PHASE_MODEL="RF_PHASE", AMPLITUDE_MODEL="RF_AMPLITUDE", FREQUENCY_MODEL="RF_FREQUENCY", L=100., HEIGHT=200., WIDTH=2000.;
cavity_offset: LOCAL_CARTESIAN_OFFSET, end_position_x=-50., end_position_y=0., end_normal_x=1., end_normal_y=0.;

l1: Line = (ringdef, probe1, triplet, triplet, triplet, triplet, triplet, triplet, triplet, triplet, triplet, triplet, triplet, triplet , patch_offset, cavity_offset, rf_cavity, cavity_offset);

Dist1: DISTRIBUTION, DISTRIBUTION=fromfile, FNAME="distributions/reference_particle.beam";

Fs1:FIELDSOLVER, FSTYPE=FFT, MX=32, MY=32, MT=32, 
		 PARFFTX=true, PARFFTY=true, PARFFTT=false,
		 BCFFTX=open, BCFFTY=open, BCFFTT=open,BBOXINCR=2;

// BFREQ has units of [MHz] weirdly
beam1: BEAM, PARTICLE=PROTON, pc=p0, SPACECHARGE=false, NPART=1, 
       BCURRENT=1.0E-19, CHARGE=1.0, BFREQ=1.e3/revolution_period;

// step_size = 1/BFREQ/STEPSPERTURN
track, line=l1, beam=beam1,MAXSTEPS=max_steps, STEPSPERTURN=revolution_period/step_size;
run, file = "track_output", turns = 1, method = "CYCLOTRON-T", beam=beam1,
     fieldsolver=Fs1, distribution=Dist1;
endtrack;
Stop;
